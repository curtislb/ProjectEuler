#!/usr/bin/env julia

"""
# Problem 2: Even Fibonacci numbers

Each new term in the Fibonacci sequence is generated by adding the previous two
terms. By starting with 1 and 2, the first 10 terms will be:

    1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...

By considering the terms in the Fibonacci sequence whose values do not exceed
`limit`, find the sum of the even-valued terms.
"""
module Problem002

include("Sequences.jl")

"""
    solve(; limit::Integer = 4_000_000)

Returns the solution for [`Problem002`](@ref) with the given parameters.
"""
function solve(; limit::Integer = 4_000_000)
    total = 0
    for (i, n) in Iterators.enumerate(Sequences.Fibonacci())
        # don't consider terms above limit
        if n > limit
            break
        end

        # sum every third (even) Fibonacci term
        if i % 3 == 2
            total += n
        end
    end
    return total
end

end
